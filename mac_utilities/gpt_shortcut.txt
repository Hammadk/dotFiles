tell application "Google Chrome"
	activate
	set theURL to "https://chat.openai.com" -- change to desired tab
	set found to false
	set theTabIndex to -1
	set theWindowIndex to -1

	-- Requires opening Google Chrome -> View -> Developer -> Allow JS from Apple Script
	set scriptToFocusOnTextArea to "document.getElementById('prompt-textarea').focus();"

	-- Check if there are any open windows, if not create one
	if (count windows) is equal to 0 then
		make new window
		delay 1 -- Wait a bit for the window to be created; adjust as needed
	end if

	repeat with w from 1 to (count windows)
		set theTabIndex to 0
		repeat with t from 1 to (count tabs of window w)
			set theTabIndex to theTabIndex + 1
			if the URL of tab t of window w starts with theURL then
				set found to true
				set theWindowIndex to w
				exit repeat
			end if
		end repeat
		if found then
			exit repeat
		end if
	end repeat
	if found then
		tell window theWindowIndex
			set active tab index to theTabIndex
			set index to theWindowIndex -- Brings the window with the found tab to the front
			execute tab theTabIndex javascript scriptToFocusOnTextArea
		end tell
	else
		tell window 1
			set newTab to make new tab with properties {URL:theURL}
			delay 1 -- Adjust delay to allow the page to load
			execute newTab javascript scriptToFocusOnTextArea
		end tell
	end if
end tell

